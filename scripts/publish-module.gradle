apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'A simple library that helps Android developers to execute JavaScript code from Android native side easily without using WebView.'
                    url = 'https://github.com/pavel-corsaghin/AndroidJSModule'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/pavel-corsaghin/AndroidJSModule/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'pavel-corshagin'
                            name = 'Hung Nguyen'
                            email = 'hungnm.k52tnvlkt@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/pavel-corsaghin/AndroidJSModule.git'
                        developerConnection = 'scm:git:ssh://github.com/pavel-corsaghin/AndroidJSModule.git'
                        url = 'https://github.com/pavel-corsaghin/AndroidJSModule/tree/main'
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}